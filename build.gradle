plugins {
  id 'java'
  id 'application'
  id 'com.github.johnrengelman.shadow' version '5.0.0'
}

group = 'ch.msrion'
version = '1.0.0-SNAPSHOT'

repositories {
  mavenCentral()
  jcenter()
}

ext {
  vertxVersion = '3.7.0'
  logbackVersion = '1.2.3'
  junitJupiterEngineVersion = '5.4.0'
}

application {
  mainClassName = 'io.vertx.core.Launcher'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

def mainVerticleName = 'ch.msrion.apicontract.MainVerticle'
def watchForChange = 'src/**/*'
def doOnChange = './gradlew classes'

dependencies {
  annotationProcessor "io.vertx:vertx-codegen:$vertxVersion:processor"
  annotationProcessor "io.vertx:vertx-web-api-service:$vertxVersion"

  compile "io.vertx:vertx-web-api-contract:$vertxVersion"
  compile "io.vertx:vertx-web-api-service:$vertxVersion"
  compile "io.vertx:vertx-codegen:$vertxVersion:processor"
  
  implementation "io.vertx:vertx-core:$vertxVersion"
  implementation "io.vertx:vertx-web:$vertxVersion"
  implementation "ch.qos.logback:logback-classic:$logbackVersion"

  testImplementation "io.vertx:vertx-web-client:$vertxVersion"
  testImplementation "io.vertx:vertx-junit5:$vertxVersion"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitJupiterEngineVersion"
  testImplementation "org.junit.jupiter:junit-jupiter-api:$junitJupiterEngineVersion"
}

/*
compileJava {
    options.annotationProcessorGeneratedSourcesDirectory = file("src/main/java")
}
*/

shadowJar {
  classifier = 'fat'
  manifest {
    attributes 'Main-Verticle': mainVerticleName
  }
  mergeServiceFiles {
    include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
  }
}

test {
  useJUnitPlatform()
  testLogging {
    events 'PASSED', 'FAILED', 'SKIPPED'
  }
}

run {
  args = ['run', mainVerticleName, "--redeploy=$watchForChange", "--launcher-class=$mainClassName", "--on-redeploy=$doOnChange"]
}
